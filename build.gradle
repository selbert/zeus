buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
        classpath "io.spring.gradle:propdeps-plugin:0.0.10.RELEASE"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.1"
    }
}

plugins {
    id "org.sonarqube" version "2.6.2" apply false
    id "net.ltgt.apt-idea" version "0.18" apply false
    id "net.ltgt.apt" version "0.18" apply false
    id "io.spring.dependency-management" version "1.0.5.RELEASE" apply false
}

subprojects { subproject ->
    apply plugin: 'org.sonarqube'
    apply plugin: 'net.ltgt.apt-idea'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    // Until JHipster supports JDK 9
    assert System.properties['java.specification.version'] == '1.8'

    apply plugin: 'maven'
    if (subproject.name != 'frontend') {
        apply plugin: 'org.springframework.boot'
    }
    apply plugin: 'war'
    apply plugin: 'propdeps'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom 'io.github.jhipster:jhipster-dependencies:' + jhipster_dependencies_version
        }
    }
}

wrapper {
    gradleVersion = '4.10.2'
}
